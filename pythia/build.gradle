/*
 * Copyright (c) 2015-2020, Virgil Security, Inc.
 *
 * Lead Maintainer: Virgil Security Inc. <support@virgilsecurity.com>
 *
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 *     (1) Redistributions of source code must retain the above copyright notice, this
 *     list of conditions and the following disclaimer.
 *
 *     (2) Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *
 *     (3) Neither the name of virgil nor the names of its
 *     contributors may be used to endorse or promote products derived from
 *     this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
 * FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
 * SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
 * CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
 * OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'signing'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
    // Keep runtime compatibility for existing consumers while building on JDK 17.
    withSourcesJar()
    withJavadocJar()
}

tasks.withType(JavaCompile).configureEach {
    options.release = 8
}

dependencies {
    // Virgil SDK
    api libs.virgil.sdk

    // Virgil Crypto
    api libs.virgil.crypto.pythia

    // Http
    implementation libs.gson

    // Test
    testImplementation libs.junit.jupiter
    testImplementation libs.commons.lang
    testImplementation libs.commons.codec
    testImplementation libs.mockito.core
    testImplementation libs.mockito.junit.jupiter

}

test {
    useJUnitPlatform()
    systemProperties = System.properties
}

def virgilGeneratedSourcesDir = "${buildDir}/generated/virgil-info"
sourceSets.main.java.srcDirs = [virgilGeneratedSourcesDir, "src/main/java"]

task generateVirgilInfo(type: Copy) {
    from('src/main/templates') {
        include '**/*.java'
    }
    into virgilGeneratedSourcesDir
    expand(version: "$project.version")
    includeEmptyDirs = false
}

compileJava.dependsOn(generateVirgilInfo)

// sourcesJar/javadoc can run without compileJava; ensure generated sources exist first
tasks.named("sourcesJar").configure {
    dependsOn(generateVirgilInfo)
}
tasks.named("javadoc").configure {
    dependsOn(generateVirgilInfo)
}

def authenticationUsername = (findProperty('authentication_username') ?: System.getenv('authentication_username'))?.toString()
def authenticationPassword = (findProperty('authentication_password') ?: System.getenv('authentication_password'))?.toString()

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'pythia'
            from components.java

            pom {
                name = 'Virgil Pythia Java SDK'
                description = 'Virgil Security provides an SDK which allows you to communicate with Virgil Pythia Service.'
                url = 'https://www.virgilsecurity.com/'
                licenses {
                    license {
                        name = 'Virgil Security, Inc. license'
                        url = 'https://github.com/VirgilSecurity/virgil-pythia-kotlin/blob/master/LICENSE.txt'
                    }
                }
                developers {
                    developer {
                        id = 'virgil-security-inc'
                        name = 'Virgil Security, Inc.'
                        email = 'support@virgilsecurity.com'
                        organizationUrl = 'https://www.virgilsecurity.com/'
                    }
                }
                scm {
                    connection = 'scm:git:https://github.com/VirgilSecurity/virgil-pythia-kotlin.git'
                    developerConnection = 'scm:git:git@github.com:VirgilSecurity/virgil-pythia-kotlin.git'
                    url = 'https://github.com/VirgilSecurity/virgil-pythia-kotlin'
                }
            }
        }
    }
    repositories {
        maven {
            // Used by GitHub Actions publish-release workflow (Sonatype Central Portal bundle upload).
            name = "CentralBundle"
            url = rootProject.layout.buildDirectory.dir("central-bundle-repo")
        }
        maven {
            def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = authenticationUsername
                password = authenticationPassword
            }
        }
    }
}

signing {
    required { gradle.taskGraph.allTasks.any { it.name.toLowerCase().contains("publish") } }
    def signingKey = (findProperty("signingKey") ?: System.getenv("ORG_GRADLE_PROJECT_signingKey"))?.toString()
    def signingPassword = (findProperty("signingPassword") ?: System.getenv("ORG_GRADLE_PROJECT_signingPassword"))?.toString()
    if (signingKey != null && !signingKey.isBlank()) {
        useInMemoryPgpKeys(signingKey, signingPassword)
    }
    sign publishing.publications.mavenJava
}
