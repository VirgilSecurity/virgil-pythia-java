/*
 * Copyright (c) 2015-2020, Virgil Security, Inc.
 *
 * Lead Maintainer: Virgil Security Inc. <support@virgilsecurity.com>
 *
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions are met:
 *
 *     (1) Redistributions of source code must retain the above copyright notice, this
 *     list of conditions and the following disclaimer.
 *
 *     (2) Redistributions in binary form must reproduce the above copyright notice,
 *     this list of conditions and the following disclaimer in the documentation
 *     and/or other materials provided with the distribution.
 *
 *     (3) Neither the name of virgil nor the names of its
 *     contributors may be used to endorse or promote products derived from
 *     this software without specific prior written permission.
 *
 * THIS SOFTWARE IS PROVIDED BY THE COPYRIGHT HOLDERS AND CONTRIBUTORS "AS IS"
 * AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
 * IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
 * DISCLAIMED. IN NO EVENT SHALL THE COPYRIGHT HOLDER OR CONTRIBUTORS BE LIABLE
 * FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR CONSEQUENTIAL
 * DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF SUBSTITUTE GOODS OR
 * SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS INTERRUPTION) HOWEVER
 * CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY,
 * OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE
 * OF THIS SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

plugins {
    alias(libs.plugins.android.library)
    alias(libs.plugins.kotlin.android)
    alias(libs.plugins.dokka)
    id 'maven-publish'
    id 'signing'
}

android {
    namespace "com.virgilsecurity.pythia.android"
    compileSdk 34

    defaultConfig {
        minSdk 21
        targetSdk 34
        consumerProguardFiles 'proguard-rules.txt'
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    packaging {
        resources {
            excludes += [
                    'META-INF/LICENSE.md',
                    'META-INF/LICENSE-notice.md'
            ]
        }
    }

    publishing {
        singleVariant("release") {
            withSourcesJar()
        }
    }
}

dependencies {
    // Inner dependencies
    api (project(':pythia')) {
        exclude group: 'com.virgilsecurity.crypto', module: 'pythia'

        exclude group: 'com.virgilsecurity.sdk', module: 'sdk'
    }

    // Virgil SDK
    api libs.virgil.sdk.android

    // Virgil Crypto
    api libs.virgil.crypto.pythia.android
    api libs.virgil.crypto.foundation.android // TODO remove after fixed in cryptowrapper
}

def authenticationUsername = (findProperty('authentication_username') ?: System.getenv('authentication_username'))?.toString()
def authenticationPassword = (findProperty('authentication_password') ?: System.getenv('authentication_password'))?.toString()

tasks.named("dokkaJavadoc").configure {
    outputDirectory.set(file("$buildDir/dokka/javadoc"))
}

def javadocJar = tasks.register("javadocJar", Jar) {
    dependsOn(tasks.named("dokkaJavadoc"))
    archiveClassifier.set("javadoc")
    from(tasks.named("dokkaJavadoc").flatMap { it.outputDirectory })
}

afterEvaluate {
    publishing {
        publications {
            mavenJava(MavenPublication) {
                artifactId = 'pythia-android'
                from components.release
                artifact(javadocJar)

                pom {
                    name = 'Virgil Pythia Android SDK'
                    description = 'Virgil Security provides an SDK which allows you to communicate with Virgil Pythia Service.'
                    url = 'https://www.virgilsecurity.com/'
                    licenses {
                        license {
                            name = 'Virgil Security, Inc. license'
                            url = 'https://github.com/VirgilSecurity/virgil-pythia-kotlin/blob/master/LICENSE.txt'
                        }
                    }
                    developers {
                        developer {
                            id = 'virgil-security-inc'
                            name = 'Virgil Security, Inc.'
                            email = 'support@virgilsecurity.com'
                            organizationUrl = 'https://www.virgilsecurity.com/'
                        }
                    }
                    scm {
                        connection = 'scm:git:https://github.com/VirgilSecurity/virgil-pythia-kotlin.git'
                        developerConnection = 'scm:git:git@github.com:VirgilSecurity/virgil-pythia-kotlin.git'
                        url = 'https://github.com/VirgilSecurity/virgil-pythia-kotlin'
                    }
                }
            }
        }

        repositories {
            maven {
                // Used by GitHub Actions publish-release workflow (Sonatype Central Portal bundle upload).
                name = "CentralBundle"
                url = rootProject.layout.buildDirectory.dir("central-bundle-repo")
            }
            maven {
                def releasesRepoUrl = "https://oss.sonatype.org/service/local/staging/deploy/maven2/"
                def snapshotsRepoUrl = "https://oss.sonatype.org/content/repositories/snapshots"
                url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
                credentials {
                    username = authenticationUsername
                    password = authenticationPassword
                }
            }
        }
    }
    signing {
        required { gradle.taskGraph.allTasks.any { it.name.toLowerCase().contains("publish") } }
        def signingKey = (findProperty("signingKey") ?: System.getenv("ORG_GRADLE_PROJECT_signingKey"))?.toString()
        def signingPassword = (findProperty("signingPassword") ?: System.getenv("ORG_GRADLE_PROJECT_signingPassword"))?.toString()
        if (signingKey != null && !signingKey.isBlank()) {
            useInMemoryPgpKeys(signingKey, signingPassword)
        }
        sign publishing.publications.mavenJava
    }
}
